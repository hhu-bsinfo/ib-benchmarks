import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
}

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = ['src']

apply plugin: 'c'

model {
    components {
        IbPerfCounter(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir "src/IbPerfCounter"
                        include "**/*.c"
                    }

                    exportedHeaders {
                        srcDirs "src/IbPerfCounter",
                                "${Jvm.current().javaHome}/include",
                                "${Jvm.current().javaHome}/include/linux"
                    }
                }
            }

            binaries.all {
                cCompiler.args << "-std=c99"
                cCompiler.args << "-I/usr/include/infiniband"
                linker.args << "-libverbs"
                linker.args << "-libmad"
            }
        }
    }
}

compileJava.dependsOn "IbPerfCounterSharedLibrary"

jar {
    from('build/libs/ibPerfCounter/shared/libIbPerfCounter.so') {
        include 'libIbPerfCounter.so'
    }

    manifest {
        attributes 'Main-Class': 'JSocketBench'
    }
}

repositories {
    mavenCentral()
}

task wrapper (type: Wrapper) {
    gradleVersion = "4.9"
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
